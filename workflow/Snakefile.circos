# Circos plot for a custom genome assembly and annotation

# Required input files (expected paths):
# 1. Genome assembly:
#    results/fcs/assembly/{assembly_name}.asm.bp.p_ctg.fa
# 2. Gene annotation (GFF3):
#    results/braker3/{assembly_name}/braker.gff3
# 3. RepeatMasker output (.out.xm) [optional]:
#    results/repeatmasker/{assembly_name}.asm.bp.p_ctg.fa.out.xm
#    If not present, it will be generated by running RepeatModeler and RepeatMasker.

# Add `-s workflow/Snakefile.circos` option to Snakemake command line to run this workflow.

containerized: "docker://aurelia01/genome_assembly_pipeline:latest"
configfile: "config/config.yml"
include: "rules/softmask.smk"
include: "rules/circos_plot.smk"

rule extract_long_contigs:
    input:
        assembly = "results/fcs/assembly/{assembly_name}.asm.bp.p_ctg.fa",
        config_stamp = "config/config.yml"
    output:
        "results/fcs/assembly_long_contigs/{assembly_name}.asm.bp.p_ctg.fa"
    log:
        out = "logs/extract_long_contigs_fcs_{assembly_name}.out",
        err = "logs/extract_long_contigs_fcs_{assembly_name}.err"
    conda:
        "envs/seqkit.yml"
    params:
        min_long_contig_length = config["min_long_contig_length"]
    shell:
        """
        (
            seqkit seq --min-len {params.min_long_contig_length} {input.assembly} \
            | seqkit sort --by-length --reverse \
            > {output}
        ) > {log.out} 2> {log.err}
        """

rule all:
    input:
        expand("results/circos_plot/{assembly_name}_circos_plot.pdf", assembly_name=[config["assembly_name"]]),
        expand("results/circos_plot/{assembly_name}_linear_plot.pdf", assembly_name=[config["assembly_name"]])