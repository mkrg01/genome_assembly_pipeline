containerized: "docker://aurelia01/genome_assembly_pipeline:v0.4.2"
configfile: "config/config.yml"

assembly_name = config["assembly_name"]

def seqkit_stats_organelle_path():
    mito_txt = "results/oatk/seqkit/{assembly_name}_mito_seqkit_stats.txt"
    mito_tsv = "results/oatk/seqkit/{assembly_name}_mito_seqkit_stats.tsv"
    pltd_txt = "results/oatk/seqkit/{assembly_name}_pltd_seqkit_stats.txt"
    pltd_tsv = "results/oatk/seqkit/{assembly_name}_pltd_seqkit_stats.tsv"
    if config["oatk_organelle"] == "mito":
        return {"mito_txt": mito_txt, "mito_tsv": mito_tsv}
    elif config["oatk_organelle"] == "pltd":
        return {"pltd_txt": pltd_txt, "pltd_tsv": pltd_tsv}
    elif config["oatk_organelle"] == "mito_and_pltd":
        return {"mito_txt": mito_txt, "mito_tsv": mito_tsv, "pltd_txt": pltd_txt, "pltd_tsv": pltd_tsv}
    else:
        raise ValueError("Invalid value for 'oatk_organelle' in config.yml. Must be one of 'mito', 'pltd', or 'mito_and_pltd'.")

def seqkit_stats_organelle_real_path(assembly_name):
    return [v.format(assembly_name=assembly_name) for v in seqkit_stats_organelle_path().values()]

include: "rules/genome_assembly.smk"
include: "rules/softmask.smk"
include: "rules/gene_prediction.smk"
include: "rules/circos_plot.smk"

# Step 1: Genome assembly
def assembly_all_inputs(assembly_name):
    return [
        f"results/hifi_reads/smudgeplot/{assembly_name}_masked_errors_smu.txt",
        f"results/hifi_reads/genomescope2/{assembly_name}_summary.txt",
        *seqkit_stats_organelle_real_path(assembly_name),
        f"results/hifiasm/seqkit/{assembly_name}_seqkit_stats.tsv",
        f"results/hifiasm/length/{assembly_name}_length.pdf",
        f"results/hifiasm/gc_content/{assembly_name}_gc_content.pdf",
        f"results/hifiasm/busco_genome/BUSCO_{assembly_name}.asm.bp.p_ctg.fa",
        f"results/hifiasm/merqury/{assembly_name}.merqury.qv",
        f"results/hifiasm/depth/{assembly_name}_contig_depth.pdf",
        f"results/hifiasm/tidk/{assembly_name}_tidk_find.svg",
        f"results/hifiasm/tidk/{assembly_name}_tidk_explore.tsv",
        f"results/hifiasm/tidk/{assembly_name}_tidk_search.svg"
    ]

# Step 2: Organelle removal
def remove_organelle_all_inputs(assembly_name):
    return assembly_all_inputs(assembly_name) + [
        f"results/organelle_removal/seqkit/{assembly_name}_seqkit_stats.tsv",
        f"results/organelle_removal/length/{assembly_name}_length.pdf",
        f"results/organelle_removal/gc_content/{assembly_name}_gc_content.pdf",
        f"results/organelle_removal/busco_genome/BUSCO_{assembly_name}.asm.bp.p_ctg.fa",
        f"results/organelle_removal/merqury/{assembly_name}.merqury.qv",
        f"results/organelle_removal/depth/{assembly_name}_contig_depth.pdf",
        f"results/organelle_removal/tidk/{assembly_name}_tidk_find.svg",
        f"results/organelle_removal/tidk/{assembly_name}_tidk_explore.tsv",
        f"results/organelle_removal/tidk/{assembly_name}_tidk_search.svg",
        f"results/hifiasm/map_to_organelle/organelle_contig_depth/{assembly_name}_contig_depth.pdf"
    ]

# Step 3: Contamination removal by FCS
def remove_contamination_all_inputs(assembly_name):
    return remove_organelle_all_inputs(assembly_name) + [
        f"results/fcs/seqkit/{assembly_name}_seqkit_stats.tsv",
        f"results/fcs/length/{assembly_name}_length.pdf",
        f"results/fcs/gc_content/{assembly_name}_gc_content.pdf",
        f"results/fcs/busco_genome/BUSCO_{assembly_name}.asm.bp.p_ctg.fa",
        f"results/fcs/merqury/{assembly_name}.merqury.qv",
        f"results/fcs/depth/{assembly_name}_contig_depth.pdf",
        f"results/downloads/tidk/.{assembly_name}_.local_share_tidk_successfully_removed_or_restored.txt"
    ]

# Step 4: Soft-masking
def softmask_all_inputs(assembly_name):
    return remove_contamination_all_inputs(assembly_name) + [
        f"results/repeatmasker/{assembly_name}.asm.bp.p_ctg.fa.masked"
    ]

# Step 5: Gene prediction
def gene_prediction_all_inputs(assembly_name):
    return softmask_all_inputs(assembly_name) + [
        f"results/isoforms/busco_proteins/BUSCO_{assembly_name}_aa.fa",
        f"results/longest_cds/busco_proteins/BUSCO_{assembly_name}_aa.fa",
        f"results/isoforms/seqkit/{assembly_name}_seqkit_stats.tsv",
        f"results/longest_cds/seqkit/{assembly_name}_seqkit_stats.tsv",
        f"results/longest_cds/omark/{assembly_name}_omark"
    ]

# Step 6: Circos plot
def circos_plot_all_inputs(assembly_name):
    return gene_prediction_all_inputs(assembly_name) + [
        f"results/circos_plot/{assembly_name}_circos_plot.pdf",
        f"results/circos_plot/{assembly_name}_linear_plot.pdf"
    ]

rule all:
    input:
        *circos_plot_all_inputs(assembly_name)

rule assembly_all:
    input:
        *assembly_all_inputs(assembly_name)

rule remove_organelle_all:
    input:
        *remove_organelle_all_inputs(assembly_name)

rule remove_contamination_all:
    input:
        *remove_contamination_all_inputs(assembly_name)

rule softmask_all:
    input:
        *softmask_all_inputs(assembly_name)

rule gene_prediction_all:
    input:
        *gene_prediction_all_inputs(assembly_name)

rule circos_plot_all:
    input:
        *circos_plot_all_inputs(assembly_name)
